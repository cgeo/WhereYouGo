buildscript {
    repositories {
        jcenter()
        google()
    }
    // these dependencies are used by gradle plugins, not by the project
    dependencies {
        // Android gradle plugin
        classpath 'com.android.tools.build:gradle:4.1.2'
    }
}

plugins {
    // check for updates of gradle plugin dependencies
    id 'com.github.ben-manes.versions' version '0.38.0'
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "menion.android.whereyougo"
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 29

        versionName versionNameFromDate()
        versionCode versionCodeFromDate(0)

        // include only those language resources from libraries that we actively maintain ourselves in the translation project
        // when changed here - also change in array.xml/strings_pref_languages.xml is necessary
        // not yet enough strings translated (>40%): "sl","sv"
        resConfigs "en","ar","ca","cs","da","de", "el","es","fi","fr","hu","it","ja","ko","nb","nl","pl","pt-rBR","pt-rPT","ru","sk","tr"
    }

    // define ndk version to use local the same version as on CI
    ndkVersion '21.0.6113669'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true

            // debug build name contains git commit for better reproduction of bugs
            versionNameSuffix "-" + GitCommit.currentShort() + " developer build"
        }
        nightly {
            def nightlyName = System.getenv('NB') // NB, NB1, ...
            versionNameSuffix "-$nightlyName-" + GitCommit.currentShort()
           // use release version of source code library dependencies
            matchingFallbacks = ['release']
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    packagingOptions {
        // exclude ARMEABI native so file, ARMEABI has been removed in NDK r17.
        exclude "lib/armeabi/**"
    }

    // special version code for nightly and rc builds
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'nightly') {
            variant.outputs.all {
                setVersionCodeOverride(versionCodeFromDate(10000000))
            }
        }
        // debug and release builds have offset zero, no special handling here
    }
}

dependencies {
    fileTree(dir: 'libs', include: ['*.jar'])

    // Legacy Support Library
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Locus Maps integration
    //noinspection GradleDependency
    implementation 'com.asamm:locus-api-android:0.2.7'

    // openwig@cgeo
    implementation 'com.github.cgeo:openwig:1.0.0'

    // File select dialog
    implementation 'com.github.jfmdev:aFileDialog:v1.0.6'

    //
    implementation 'com.github.jspricke.mapsforge-map-whereyougo:mapsforge-map:75d7c20'

    // barcode scanner
    // v4.x: Minimum SDK version 24, downgradable to 14 by using zxing:core 3.3.0, uses androidx
    implementation 'com.journeyapps:zxing-android-embedded:4.1.0'
    // downgrade zxing:core to 3.3.0 or earlier for Android 14+ support
    // AndroidManifest.xml: use tools:overrideLibrary="com.google.zxing.client.android" to force usage
    //noinspection GradleDependency
    implementation 'com.google.zxing:core:3.3.0'

    // Okhttp network access. 3.12.x is API level < 21, 3.13 or better requires API 21
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'

    // ani gif support
    // minSdk=17 from version 1.2.16
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.15'
}

/*
 * version number from the current date, plus an offset defined by the build type (to define which versions overwrite each other)
 */
static def versionCodeFromDate(offset) {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return Integer.valueOf(formattedDate) + offset
}

/*
 * version name based on current date
 */
static def versionNameFromDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}

class GitCommit {
    static String commitId

    /*
     * get the most recent git commit ID
     */
    static String current() {
        if (commitId == null) {
            Process p = ['git', 'rev-parse', 'HEAD'].execute()
            if (p.waitFor() != 0) {
                throw new Error("Unable to get git commit id: " + p.err.text)
            }
            commitId = p.text.trim()
        }
        return commitId
    }

    static String currentShort() {
        return current().substring(0, 7)
    }
}

/*
 * Configuration for dependencyUpdates, see https://github.com/ben-manes/gradle-versions-plugin
 */
tasks.named('dependencyUpdates') {
    checkForGradleUpdate = true
    revision = "release"
    gradleReleaseChannel = "current"
}
