/*
 * Build scan plugin shall be applied first
 */
apply plugin: 'com.gradle.build-scan'

buildscript {
    repositories {
        jcenter()
        // gradle build-scan for profiling our build script
        maven {
            url 'https://plugins.gradle.org/m2'
        }
        google()
    }
    // these dependencies are used by gradle plugins, not by the project
    dependencies {
        // check for updates of gradle plugin dependencies
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'

        // Android gradle plugin
        classpath 'com.android.tools.build:gradle:3.5.3'

        // gradle build script profiling
        classpath 'com.gradle:build-scan-plugin:2.4.2'
    }
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "menion.android.whereyougo"
        versionCode 45
        versionName "0.10.0"
        minSdkVersion 16
        targetSdkVersion 28
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    packagingOptions {
        // exclude ARMEABI native so file, ARMEABI has been removed in NDK r17.
        exclude "lib/armeabi/**"
    }
}

dependencies {
    fileTree(dir: 'libs', include: ['*.jar'])
    // Support Library AppCompat
    implementation 'com.android.support:support-v4:28.0.0'

    // Locus Maps integration
    //noinspection GradleDependency
    implementation 'com.asamm:locus-api-android:0.2.7'

    // openwig with extentions
    implementation 'com.github.biylda:openwig:3a1e405'

    // File select dialog
    implementation 'com.github.jfmdev:aFileDialog:v1.0.6'

    //
    implementation 'com.github.jspricke.mapsforge-map-whereyougo:mapsforge-map:75d7c20'

    //
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // Okhttp network access. 3.12.x is API level < 21, 3.13 or better requires API 21
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.6'

    // ani gif support
    // minSdk=17 from version 1.2.16
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.15'
}
